///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать fs
// #Использовать "../../../../core"

Перем Конфигуратор;

Перем ИмяОбработкиВыгрузкиЗагрузки;
Перем КаталогОбработок;

Перем ЛогПриложения;
Перем ЛогЗагрузкиОбработок;

Процедура ОписаниеКоманды(Знач Команда) Экспорт

	УстановитьПараметрыКоманды(Команда);
	УстановитьКоманды(Команда);

КонецПроцедуры // НастроитьКоманду

Процедура УстановитьКоманды(Знач Команда)

КонецПроцедуры

Процедура УстановитьПараметрыКоманды(Знач Команда)

	Команда.Аргумент("PROCESSOR", , "Путь к обработке, которая будет запущена для загрузки внешних обработок")
		.ТСтрока()
	;

	Команда.Аргумент("PATH", , "Каталог, из которого будут загружены внешние обработки")
		.ТСтрока()
	;

	Команда.Спек = "PROCESSOR PATH";

КонецПроцедуры


// Выполняет присваивание значений входящих параметров переменным модуля
//
// Параметры:
//	Команда - КомандаПриложения - выполняемая команда
//
Процедура ПрочитатьВходящиеПараметры(Знач Команда)

	ПараметрыПриложения.УстановитьПараметрыПриложения(Команда.Приложение);

	ИмяОбработкиВыгрузкиЗагрузки = Команда.ЗначениеАргумента("PROCESSOR");
	КаталогОбработок = Команда.ЗначениеАргумента("PATH");

	ЛогЗагрузкиОбработок = ОбъединитьПути(ПараметрыПриложения.КаталогЛогов(), "load_processors.log");

КонецПроцедуры

Процедура ПодготовитьСлужебныеФайлы()

	ФС.ОбеспечитьКаталог(КаталогОбработок);

	ОбщийФункционал.ОчиститьТекстовыйФайл(ЛогЗагрузкиОбработок);

КонецПроцедуры

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   Приложение - Модуль - Модуль менеджера приложения
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПрочитатьВходящиеПараметры(Команда);
	ПодготовитьСлужебныеФайлы();

	Конфигуратор = ПараметрыПриложения.ПодключитьКонфигуратор();

	ВыполнитьЗагрузкуВнешнихОбработок();
	
КонецПроцедуры // ВыполнитьКоманду

Процедура ВыполнитьЗагрузкуВнешнихОбработок()

	ЛогПриложения.Информация("Начало загрузки внешних обработок в ИБ");

	Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(ЛогЗагрузкиОбработок, Ложь);
	
	ПараметрыОбработки = ПараметрыЗапускаВнешнейОбработки();
	ПараметрыОбработки.РежимРаботы = "Загрузка";
	ПараметрыОбработки.КаталогОбработок = КаталогОбработок;
	// BSLLS:MissingTemporaryFileDeletion-off
	ПараметрыОбработки.ПутьКЛогам = ПолучитьИмяВременногоФайла(".txt");
	// BSLLS:MissingTemporaryFileDeletion-on
	КлючЗапуска = КлючЗапускаВнешнейОбработки(ПараметрыОбработки);

	ВыполнениеОбработки = СтрШаблон("/execute""%1""", ИмяОбработкиВыгрузкиЗагрузки);
	Результат = Истина;
	Попытка
		Конфигуратор.ЗапуститьВРежимеПредприятия(КлючЗапуска, , ВыполнениеОбработки);
	Исключение
		Результат = Ложь;
	КонецПопытки;

	СкопироватьЛогИзВременногоФайла(ПараметрыОбработки.ПутьКЛогам, ЛогЗагрузкиОбработок);

	ТекстСообщения = "Загрузка внешних обработок в ИБ завершена %1";
	Если Результат Тогда
		ЛогПриложения.Информация(СтрШаблон(ТекстСообщения, "успешно"));
	Иначе
		ЛогПриложения.Информация(СтрШаблон(ТекстСообщения, "с ошибками"));
	КонецЕсли;

КонецПроцедуры

Процедура СкопироватьЛогИзВременногоФайла(Источник, Приемник)

	ЛогПлатформы = Новый ЧтениеТекста(Источник);
	ЛогОбработки = Новый ЗаписьТекста(Приемник, , , Истина);
	ЛогОбработки.ЗаписатьСтроку(ЛогПлатформы.Прочитать());

	ЛогПлатформы.Закрыть();
	ЛогОбработки.Закрыть();

	УдалитьФайлы(Источник);

КонецПроцедуры

Функция ПараметрыЗапускаВнешнейОбработки()
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("РежимРаботы");
	Параметры.Вставить("КаталогОбработок");
	Параметры.Вставить("ПутьКЛогам");
	Параметры.Вставить("ЗавершитьРаботуСистемы");
	
	Возврат Параметры;
	
КонецФункции

Функция КлючЗапускаВнешнейОбработки(Параметры)
	
	КлючЗапуска = "";
	Для Каждого КлючИЗначение Из Параметры Цикл
		КлючЗапуска = КлючЗапуска
			+ ?(Не ПустаяСтрока(КлючЗапуска), ";", "")
			+ СтрШаблон("%1&%2", КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Возврат КлючЗапуска;
	
КонецФункции

ЛогПриложения = ПараметрыПриложения.Лог();
