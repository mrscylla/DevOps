///////////////////////////////////////////////////////////////////////////////
//
// Рекомендованная структура модуля точки входа приложения
//
///////////////////////////////////////////////////////////////////////////////

#Использовать cli
#Использовать "."
// #Использовать "../core"

///////////////////////////////////////////////////////////////////////////////
Перем Приложение;

Функция КаталогЛогов() Экспорт

    Возврат Приложение.ПолучитьКоманду().ЗначениеОпции("logs");

КонецФункции

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	КомандаПриложения.ВывестиСправку();
КонецПроцедуры


Процедура ВыполнитьПриложение()

	ИмяПродукта = ПараметрыПриложения.ИмяПродукта();

    Приложение = Новый КонсольноеПриложение(ИмяПродукта, "Управление конфигурацией и внешними обработками 1С с помощью git");
    Приложение.Версия("v version", ПараметрыПриложения.ВерсияПродукта());

    УстановитьПараметрыКоманды(Приложение);
    УстановитьКоманды(Приложение);

    ПараметрыПриложения.УстановитьПриложение(Приложение);

    Приложение.Запустить(АргументыКоманднойСтроки);

КонецПроцедуры // ВыполнениеКоманды()

Процедура УстановитьКоманды(Знач Приложение)

    Приложение.ДобавитьКоманду("config", 
        "Группа команд для управления конфигурацией", 
        Новый КомандаConfig)
    ;

    Приложение.ДобавитьКоманду("processors procs", 
        "Группа команд для управления внешними обработками", 
        Новый КомандаProcessors)
    ;

    Приложение.УстановитьОсновноеДействие(ЭтотОбъект);

КонецПроцедуры

Процедура УстановитьПараметрыКоманды(Знач Приложение)

    Приложение.Опция("con connection", , "Путь к файлу с параметрами подключения к базе")
        .ТСтрока()
        .Обязательный(Истина)
    ;

    Приложение.Опция("l logs", , "Путь к каталогу для сохранения логов платформы")
        .ТСтрока()
        .Обязательный(Истина)
    ;

    Приложение.Опция("s srvname", , "Имя сервера 1С")
        .ТСтрока()
        .Обязательный()
    ;

    Приложение.Опция("b ibname", , "Имя базы данных 1С")
        .ТСтрока()
        .Обязательный()
    ;

    Приложение.Опция("u user", , "Имя пользователя")
        .ТСтрока()
        .Обязательный()
    ;

    Приложение.Опция("p pwd", , "Пароль пользователя")
        .ТСтрока()
        .Обязательный()
    ;

    Приложение.Опция("uc", , "Код разблокировки")
        .ТСтрока()
    ;

    // Обязательный() для опций не работает >_<
    // fix: https://github.com/Morkhe/cli/commit/b42b1838b09984180a003962367e0f181aee63cf
    Приложение.УстановитьСпек("--connection | (--logs --srvname --ibname --user --pwd [--uc])");

КонецПроцедуры
    
Попытка
    ВыполнитьПриложение();
Исключение
    Сообщить(ОписаниеОшибки());
КонецПопытки;
